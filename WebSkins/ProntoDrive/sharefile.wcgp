// ============================================= //
//                   List files                  //
//                                               //
// Version 1.0                                   //
// Anton Katsarov <anton@webface.bg>             //
// ============================================= //
//
// Description:
//  Lists Accounts files and folders
//

function list(path) external;
function UnixTimestamp(dateTime) external;
function prontodrive::addSharePass(filepath, pass) external;

entry main is
  account = MyEmail();
  if IsString(account) then
    submit = GetHTTPParameter("submit",0);
    if IsString(submit) then
      files = NewArray();
      for i = 0  while i < 1000 by i+= 1 loop
        fileName = GetHTTPParameter("file",i);
        exitif fileName == null;
        files.addElement(fileName);
      end loop;
      sharedFiles = GetAccountPreferences("SharedFiles");
      sharedFilesExpire = GetAccountPreferences("SharedFilesExpire");
      expires = GetHTTPParameter("expires",0);
      if IsString(expires) then
        sharedFilesExpire = expires;
      end if;
      if !IsString(sharedFilesExpire) || sharedFilesExpire == "" then
        // Defaults to one month
        sharedFilesExpire = "2592000";
      end if;
      if !IsDictionary(sharedFiles) then
        sharedFiles = NewDictionary();
      end if;
      query = GetHTTPQuery();
      downloadHash = CryDigest("MD5", Base64Decode(Base64Encode(account + query)));
      downloadHash = ObjectToJSON(Convert(downloadHash, "hex"));
      downloadHash = Substring(String(downloadHash), 1, Length(downloadHash) - 4);
      sharedFiles.(downloadHash) = NewDictionary();
      sharedFiles.(downloadHash).file = files;
      sharedFiles.(downloadHash).expires = sharedFilesExpire;
      sharedFiles.(downloadHash).LastUpdated = UnixTimestamp(GMTTime());
      status = SetAccountPreferences(sharedFiles,"SharedFiles");
      for i = 0  while i < Length(files) by i+= 1 loop
         result = prontodrive::addSharePass("private" + files[i], downloadHash);
      end loop;
      SetHTTPResponseCode(301);
      AddHTTPResponseField("Location","/sys/ProntoDriveShared.wcgp/" + account + "/" + downloadHash);
      //SetHTTPResponseData(ObjectToJSON(sharedfiles));
    else
       empty = NewDictionary();
       ProcessWSSP("sharefile", empty);
    end if;
  end if;
end entry;
